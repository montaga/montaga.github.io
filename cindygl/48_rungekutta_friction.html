
<!DOCTYPE html>
<html>

<head>
<title>Runge-Kutta</title>
<meta charset="UTF-8">
<script type="text/javascript" src="Cindy.js"></script>
<script type="text/javascript" src="CindyGL.js"></script>
<script id="csinit" type="text/x-cindyscript">
N = 10; //number of bodies
h = 5e-3; //stepsize

L = [.5,-.5];
R = [2*N+.5,-.5];
evalf(y, alpha, k, res) := ( //evals f(y+alpha*k) where f(y)=y'. Here y = (x_1,...x_N, v_1,...v_N) and saves result to res
  colorplot(L, R, res,
    i = round(#.x);
    if(i <= N,
      imagergba(L, R, y, #+(N, 0), interpolate->false) + alpha*imagergba(L, R, k, #+(N, 0), interpolate->false) //y_i'=x_i' = v_i = y_(i+N)
    ,
      vi =  imagergba(L, R, y, (i,0), interpolate->false) + alpha*imagergba(L, R, k, (i,0), interpolate->false);
      i = i - N;
      //y_(i+N)' = v_i' = a_i = F_i/m_i
      sum = [0,0,0,0];
      xi =  imagergba(L, R, y, (i,0), interpolate->false) + alpha*imagergba(L, R, k, (i,0), interpolate->false);
      repeat(N, j, if(i!=j,
        xj =  imagergba(L, R, y, (j,0), interpolate->false)  + alpha*imagergba(L, R, k, (j,0), interpolate->false);
        sum = sum + (xj-xi)/max(|xi-xj|^3,1e-2);
      ));
      
      (sum - .03*vi - xi) //friction + drag to middle
      //sum
    )
  );
);
createimage("y", 2*N, 1);
createimage("k1", 2*N, 1);
createimage("k2", 2*N, 1);
createimage("k3", 2*N, 1);
createimage("k4", 2*N, 1);



RKstep() := (
  evalf("y", 0, "y", "k1");
  evalf("y", h/2, "k1", "k2");
  evalf("y", h/2, "k2", "k3");
  evalf("y", h, "k3", "k4");
  colorplot(L, R, "y",
    imagergba(L, R, "y", #, interpolate->false) +
    h/6*(
      imagergba(L, R, "k1", #, interpolate->false) +
      2*imagergba(L, R, "k2", #, interpolate->false) +
      2*imagergba(L, R, "k3", #, interpolate->false) +
      imagergba(L, R, "k4", #, interpolate->false)
    )
  );
);

colorplot(L, R, "y", if(#.x <= N, [random(),random(),random(), .5], [0,0,0,0]));

</script>
<script id="csmousedown" type="text/x-cindyscript">
colorplot(L, R, "y", if(#.x <= N, 2*[random()-.5,random()-.5,random()-.5, 0], [0,0,0,0]));
</script>
<script id="csdraw" type="text/x-cindyscript">
RKstep();
x = apply(1..N, i,
  imagergba(L, R, "y", (i, 0)) ++ [i];
);

forall(sort(x, -#_3), p,
  draw((p_1, p_2)/(p_3+2), color->hue(p_5/N), size->10/(p_3+2));
);

//drawimage((-1,-1),(1,-1),"y");
</script>
<script type="text/javascript">
var cdy = CindyJS({
  ports: [{id: "CSCanvas", transform:[{visibleRect:[-1,-1,1,1]}]}],
  scripts: "cs*",
  language: "en",
  autoplay: true,
  geometry: [],
  use: ["CindyGL"]
});
</script>
</head>

<body style="font-family:Arial;">
  <div id="CSCanvas" style="width:800px; height:800px;"></div>
</body>

</html>
