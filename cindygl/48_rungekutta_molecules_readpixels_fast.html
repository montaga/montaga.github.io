
<!DOCTYPE html>
<html>

<head>
<title>Runge-Kutta</title>
<meta charset="UTF-8">
<script type="text/javascript" src="Cindy.js"></script>
<script type="text/javascript" src="CindyGL.js"></script>
<script id="csinit" type="text/x-cindyscript">
N = 50; //number of bodies
h = 5e-2; //stepsize


linearcomb(y, alpha, k) := ( //saves y+alpha*k to "h"
  colorplot(L, R, "h",
    imagergba(L, R, y, #, interpolate->false) + alpha*imagergba(L, R, k, #, interpolate->false);
  );
  "h" //return value
);

evalf(y, res) := ( //evals f(y)=y'. Here y = (x_1,...x_N, v_1,...v_N) and saves result to res
  colorplot(L, R, res,
    i = round(#.x);
    vi = imagergba(L, R, y, (i,1), interpolate->false);
    xi = imagergba(L, R, y, (i,0), interpolate->false);
    if(#.y <= .5,
      //y_(i,0)'=x_i' = v_i = y_(i,1)
      vi
      /*if(|xi|>radius,
        vi+(radius*xi/|xi|-xi), //push inside sphere
        vi 
      )*/
      //vi - .1*xi/radius //pressure: push into middle
    ,
      //y_(i,1)' = v_i' = a_i = F_i/m_i
      sum = [0,0,0,0];
      
      repeat(N, j, if(i!=j,
        xj =  imagergba(L, R, y, (j,0), interpolate->false);
        sum = sum + (2*mod(i+j,2)-1)*(xj-xi)/max(|xi-xj|^3, 1e-2) + 1e-6*(xi-xj)/max(|xi-xj|^12, 1e-8); //sgn depends on charge (odd i=positive, even i = negative)
      ));
      
      ai = sum - .8*vi; //friction
      ai + .1*[random()-.5, random()-.5, random()-.5, 0] //temperature
    )
  );
);

RKstep() := (
  evalf("y", "k1");
  evalf(linearcomb("y", h/2, "k1"), "k2");
  evalf(linearcomb("y", h/2, "k2"), "k3");
  evalf(linearcomb("y", h, "k3"), "k4");
  
  colorplot(L, R, "y",
    imagergba(L, R, "y", #, interpolate->false) +
    h/6*(
      imagergba(L, R, "k1", #, interpolate->false) +
      2*imagergba(L, R, "k2", #, interpolate->false) +
      2*imagergba(L, R, "k3", #, interpolate->false) +
      imagergba(L, R, "k4", #, interpolate->false)
    )
  );
);



reset() := (
  RKstep(); //for some reason we need this in order to write to textures properly
  colorplot(L, R, "y", if(#.y <= .5, radius*2*[random()-.5,random()-.5,random()-.5, 1], [0,0,0,0]));
);

updateN(n):=(
  N = n;
  L = [.5,-.5];
  R = [N+.5,-.5];
  radius = max(re(N^(1/3))/3,1);
  createimage("y", N, 2);
  createimage("k1", N, 2);
  createimage("k2", N, 2);
  createimage("k3", N, 2);
  createimage("k4", N, 2);
  createimage("h", N, 2);
  createimage("p", N, 1);
  
  reset();
);

mat = [[1,0,0],[0,1,0],[0,0,1]];
dragging = false;
updateN(N);

</script>
<script id="csmousedown" type="text/x-cindyscript">
dragging = true;
</script>

<script id="csmouseup" type="text/x-cindyscript">
dragging = false;
</script>

<script id="csdraw" type="text/x-cindyscript">
if (dragging,
    dx = 3 * (sx - mouse().x); dy = 3 * (sy - mouse().y);
    mat = mat * (
        (1, 0, 0),
        (0, cos(dy), -sin(dy)),
        (0, sin(dy), cos(dy))
    ) * (
        (cos(dx), 0, -sin(dx)),
        (0, 1, 0),
        (sin(dx), 0, cos(dx))
    );
);

sx = mouse().x;
sy = mouse().y;

repeat(1,
  RKstep()
);

colorplot(L, R, "p",
  mat*imagergb(L, R, "y", #, interpolate->false)+(0,0,max(1.5*radius,2));
);

x = readpixels("p");
x = apply(1..N, i, x_i++[i]);

forall(sort(x, -#_3), p,
  draw((p_1, p_2)/(p_3), color->hue(p_5/2), size->20/(p_3));
);

drawimage((-1,-1),(1,-1),"y");

</script>
<script type="text/javascript">
var cdy = CindyJS({
  ports: [{id: "CSCanvas", transform:[{visibleRect:[-1,-1,1,1]}]}],
  scripts: "cs*",
  language: "en",
  autoplay: true,
  geometry: [],
  use: ["CindyGL"]
});
</script>
</head>

<body style="font-family:Arial;">
  <div id="CSCanvas" style="width:700px; height:700px; border:2px solid #000000;"></div>
  <button onclick="cdy.evokeCS('reset()')" type="button">Reset</button>
  
  <div>N = <input type="text" id="inp" value="50"  onkeypress="if((event.which ? event.which : event.keyCode)==13) { cdy.evokeCS('updateN('+this.value+')');}" size="80" style="font-size:18px"></div>
</body>

</html>
