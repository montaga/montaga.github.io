<!DOCTYPE html>
<html>

<head>
<title>Compose operations on images</title>
<meta charset="UTF-8">
<script type="text/javascript" src="Cindy.js"></script>
<script type="text/javascript" src="CindyGL.js"></script>
<style type="text/css">
* {
    margin: 0px;
    padding: 0px;
}
</style>
<script id="csinit" type="text/x-cindyscript">
//⧑ ⧒
functions = ["◪", "↔", "▨", "⧑", "⧒", "⧖", "◎", "◈","✖"];

video = camera video();

mode = "free";
addfun = "◪";
nargs = 0;
msg = "move the mouse to images and apply transformations by clicking.";
add(fun) := (
  mode = "+";
  addfun = fun;
  errc(fun);
  nargs = 1; //TODO 2 for add
  if(fun=="+" % fun=="⧖1" % fun=="⧖2" % fun=="⧖",
    nargs = 2;
  );
  if(nargs == 1,
    msg = "click on input for " + fun;
  );
  if(nargs == 2,
    msg = "click on second input for " + fun;
  );
  curinputs = [];
);

imgwidth = .9;
// 1: pos
// 2: re
// 3: im

free = [];

createtmp() := (
  ctmpcnt = ctmpcnt+1;
  createimage("tmp"+ctmpcnt, 256, 256);
  colorplot("tmp"+ctmpcnt, [0,0,0]);
  free = free :> ("tmp"+ctmpcnt);
);

createimage("camera", 256, 256);
colorplot("camera", [0,0,0]);

createimage("phase", 256, 256);
colorplot("phase", [0,0,0]);

N = 256;
createimage("re", N, N);
createimage("retmp", N, N);
createimage("im", N, N);
createimage("black", 1,1);

colorplot("re", [0,0,0]);
colorplot("retmp", [0,0,0]);
colorplot("im", [0,0,0]);
colorplot("black", [0,0,0]);

ctmpcnt = 0;
forall(1..20,
  createtmp();
);

inv(in, out) := (
  colorplot(out_2,
    [1,1,1] - imagergb(in_2, #)
  );
  colorplot(out_3,
    [1,1,1] - imagergb(in_3, #)
  );
);

flip(in, out) := (
  colorplot((-1,-1),(1,-1),out_2,
    imagergb((-1,-1),(1,-1),in_2, (-#.x,#.y))
  );
  colorplot((-1,-1),(1,-1),out_3,
    imagergb((-1,-1),(1,-1),in_3, (-#.x,#.y))
  );
);

strange(in, out) := (
  colorplot((0,0),(pi,0), out_2,
    if(im(log(complex(#)^32))>0,
      [1,1,1]-imagergb((0,0),(pi,0), in_2, #, interpolate->false),
      imagergb((0,0),(pi,0), in_2, #, interpolate->false)
    );  
  );
);
blur(in, out) := (
  rin = in_2;
  colorplot((-N/2,-N/2),(N/2,-N/2), out_2,
    1/9*(
      read(#) +
      read(# + (-1, 1)) +
      read(# + (0,  1)) +
      read(# + (1,  1)) +
      read(# + (-1, 0)) +
      read(# + (1,  0)) +
      read(# + (-1,-1)) +
      read(# + (0, -1)) +
      read(# + (1, -1))
    )
  );

/*
  colorplot((-N/2,-N/2),(N/2,-N/2), out_2,
    s = imagergb((-N/2,-N/2),(N/2,-N/2), in_2, (#.x,#.y), interpolate->false, repeat->true);
    s = s+imagergb((-N/2,-N/2),(N/2,-N/2), in_2, (#.x-4,#.y+1), interpolate->false, repeat->true);
    s = s+imagergb((-N/2,-N/2),(N/2,-N/2), in_2, (#.x+10,#.y), interpolate->false, repeat->true);
    s = s+imagergb((-N/2,-N/2),(N/2,-N/2), in_2, (#.x,#.y+5), interpolate->false, repeat->true);
    s/4
  );
  colorplot(out_3, [0,0,0]);*/
);

rin;
read(p) := imagergb((-N/2,-N/2),(N/2,-N/2), rin, p, interpolate->false);
sharpen(in, out) := (
  rin = in_2;
  colorplot((-N/2,-N/2),(N/2,-N/2), out_2,
    1/2*(
      10*read(#) -
      read(# + (-1, 1)) -
      read(# + (0,  1)) -
      read(# + (1,  1)) -
      read(# + (-1, 0)) -
      read(# + (1,  0)) -
      read(# + (-1,-1)) -
      read(# + (0, -1)) -
      read(# + (1, -1))
    )
  );
  /*
  colorplot((-N/2,-N/2),(N/2,-N/2), out_2,
    s = 5*imagergb((-N/2,-N/2),(N/2,-N/2), in_2, (#.x,#.y), interpolate->false, repeat->true);
    s = s-imagergb((-N/2,-N/2),(N/2,-N/2), in_2, (#.x-4,#.y+1), interpolate->false, repeat->true);
    s = s-imagergb((-N/2,-N/2),(N/2,-N/2), in_2, (#.x+10,#.y), interpolate->false, repeat->true);
    s = s-imagergb((-N/2,-N/2),(N/2,-N/2), in_2, (#.x,#.y+5), interpolate->false, repeat->true);
    s/2
  );
  colorplot(out_3, [0,0,0]);*/
);

fft(in, out, rot) := (
  colorplot("re", imagergb(in_2, #));
  colorplot("im", imagergb(in_3, #));

  readcomplex(p) := (
    re = imagergb((-N/2,-N/2),(N/2,-N/2), "re", p, interpolate->false, repeat->true); //circular domain
    im = imagergb((-N/2,-N/2),(N/2,-N/2), "im", p, interpolate->false, repeat->true); //circular domain
    re+i*im
  );

  step = N/2;

  while(step>=1,
    colorplot((-N/2,-N/2),(N/2,-N/2),"retmp",
    
      l = #; //global index
      delta = (mod(l.x,step),mod(l.y,step)); //shift
      k = (l-delta)/step; //local index (within recursion)
      x00 = readcomplex(2*l - delta + (0,0));
      x10 = readcomplex(2*l - delta + (step,0));
      x01 = readcomplex(2*l - delta + (0,step));
      x11 = readcomplex(2*l - delta + (step,step));
      
      apply(sqrt(1/4)*(
          x00
          + exp((-2*pi*i/N*step)*k.x)*x10
          + exp((-2*pi*i/N*step)*k.y)*x01
          + exp((-2*pi*i/N*step)*(k.x+k.y))*x11
        ), z, re(z)
      )
    );
    
    colorplot((-N/2,-N/2),(N/2,-N/2),"im",
    
      l = #; //global index
      delta = (mod(l.x,step),mod(l.y,step)); //shift
      k = (l-delta)/step; //local index (within recursion)
      x00 = readcomplex(2*l - delta + (0,0));
      x10 = readcomplex(2*l - delta + (step,0));
      x01 = readcomplex(2*l - delta + (0,step));
      x11 = readcomplex(2*l - delta + (step,step));
      
      apply(sqrt(1/4)*(
          x00
          + exp((-2*pi*i/N*step)*k.x)*x10
          + exp((-2*pi*i/N*step)*k.y)*x01
          + exp((-2*pi*i/N*step)*(k.x+k.y))*x11
        ), z, im(z)
      )
    );
    colorplot("re", imagergb("retmp", #, interpolate->false, repeat->true));
    
    step = step/2;
  );
  
  colorplot((0,0), (N,0), out_2, imagergb((0,0), (N,0),"re", if(rot,(N+1,N+1)-#,#), interpolate->false, repeat->true));
  colorplot((0,0), (N,0), out_3, imagergb((0,0), (N,0),"im", if(rot,(N+1,N+1)-#,#), interpolate->false, repeat->true));

  
);

add(in1, in2, out) := (
  colorplot(out_2,
    imagergb(in1_2, #)/2+imagergb(in2_2, #)/2
  );
  colorplot(out_3,
    imagergb(in1_3, #)/2+imagergb(in2_3, #)/2
  );
);

phase1(in1, in2, out) := (
  //out: in1 with phase from in2
  colorplot(out_2,
    z1 = imagergb(in1_2, #)+i*imagergb(in1_3, #);
    z2 = imagergb(in2_2, #)+i*imagergb(in2_3, #);
    apply(1..3, k, |z1_k|*re(z2_k/|z2_k|))
  );
  colorplot(out_3,
    z1 = imagergb(in1_2, #)+i*imagergb(in1_3, #);
    z2 = imagergb(in2_2, #)+i*imagergb(in2_3, #);
    apply(1..3, k, |z1_k|*im(z2_k/|z2_k|))
  );
);

phase2(in1, in2, out) := (
  phase1(in2, in1, out);
);


box(obj,A) := (
  [
    obj_1+A*[-imgwidth/2,-imgwidth/2], obj_1+A*[imgwidth/2,-imgwidth/2],
    obj_1+A*[imgwidth/2,imgwidth/2], obj_1+A*[-imgwidth/2,imgwidth/2]
  ]
);

drawobj(obj) := (
  fillpoly(box(obj, 1.05), color->[1,1,1], alpha->.6);
  drawimage(obj_1+[-imgwidth/2,-imgwidth/2], obj_1+[imgwidth/2,-imgwidth/2], (obj_2));
);


nodes0 = [
  [
      [(1,5), "camera", "black"],
      [(1,4), "pic1", "black"],
      [(1,3), "pic2", "black"],
      [(1,2), "pic4", "black"],
      [(1,1), "pic3", "black"]
  ]
];
nodes = nodes0;

/*
nodes_2 = nodes_2 ++ [
  [( -.5,0), "tmp1", "tmp1", ["◪", nodes_1_1]]
];*/

delete(node) := (
  todelete = [node];
  
  grow = true;
  while(grow,
    grow = false;
    forall(nodes, line, forall(line, node,
      if(length(node)>=4 & !contains(todelete, node),
        gen = node_4;
        org = gen_2;
        if(contains(todelete, org), todelete = todelete++[node]; grow = true;);
        if(length(gen)>=3,
          org2 = gen_3;
          if(contains(todelete, org2), todelete = todelete++[node]; grow = true;);
        );
      );
    ));
  );
  errc(todelete);
  free = free ++ flatten(apply(todelete, d, [d_2, d_3]));
  errc(nodes);
  nodes = apply(nodes, line, line -- todelete);
  
  errc(nodes);
);


addnode(addfun, inputs, line) := (
  //delete all former childs of inputs
  forall(nodes, line, forall(line, node, if(length(node)>=4,
    forall(inputs, in,
      if(contains(node_4, in) & !(node_4_1_1=="⧖" & addfun_1=="⧖"),
        errc("do delete");
        delete(node);
      );
    );
    
  )));
  
  errc(addfun);
  errc(inputs);
  errc(line);
  pos0 = sum(inputs, #, #_1)/length(inputs);
  if(addfun=="⧖1", pos0 = inputs_1_1);
  if(addfun=="⧖2", pos0 = inputs_2_1);
  pos0_1 = max(apply(inputs, k, (k_1)_1));
  
  pos = pos0+(imgwidth*1.3,0);
  while(length(free)<2,
    createtmp();
  );
  t1 = free_1;
  t2 = free_2;
  free = free_(3..length(free));
  
  nnode = [pos, t1, t2, [addfun] ++ inputs];
  while(length(nodes)<line,
    nodes = nodes:>[];
  );
  nodes_line = nodes_line :> nnode;
  mode = "free";
  msg = "";
  
  compute();

);

compute():=(
  
  if (image ready(video),
    colorplot([-1,-1],[1,-1],"camera", imagergb([-2,-1],[2,-1],video, #));
  );

  forall(nodes, line, forall(line, node,
    if(length(node)>=4,
      gen = node_4;
      org = gen_2;
      if(length(gen)>=3,
        org2 = gen_3;
        draw(org2_1, node_1, color->[0,0,0]);
        drawtext(size->35,align->"mid",(org2_1+node_1)/2, gen_1_1);
      );
      draw(org_1, node_1, color->[0,0,0]);
      drawtext(size->35,align->"mid",(org_1+node_1)/2, gen_1_1);
      
      if(gen_1=="◪",
        inv(org, node);
      );
      if(gen_1=="↔",
        flip(org, node);
      );
      if(gen_1=="◎",
        blur(org, node);
      );
      if(gen_1=="◈",
        sharpen(org, node);
      );
      if(gen_1=="▨",
        strange(org, node);
      );
      if(gen_1=="perm2",
        perm2(org, node);
      );
      if(gen_1=="⧑",
        fft(org, node, false);
      );
      if(gen_1=="⧒",
        fft(org, node, true);
      );
      if(gen_1=="+",
        add(org, org2, node);
      );
      if(gen_1=="⧖1",
        phase1(org, org2, node);
      );
      if(gen_1=="⧖2",
        phase2(org, org2, node);
      );
      
    );
  ));
);

compute();
</script>

<script id="csdraw" type="text/x-cindyscript">



forall(curinputs, node,
  fillpoly(box(node, 1.1), color->[1,.5,0], alpha->.5);
);


forall(nodes, line, forall(line, node,
  drawobj(node);
));



forall(nodes, line, forall(line, node,
  v = mouse() - node_1;
  if(|v.x|<imgwidth/2 & |v.y|<imgwidth/2,
    fillpoly(box(node, 1.1), color->[1,.5,0], alpha->.3);
    
    if(mode=="free",
      if(node_2=="camera", compute());
      drawimage(node_1+(-1,-1), node_1+(1,-1), node_2);
      mk = mod(round(arctan2(mouse()-node_1)*length(functions)/(2*pi))-1,length(functions))+1;
      forall(1..length(functions),k,
        c = node_1+imgwidth*.4*gauss(exp(k*2*pi*i/length(functions)));
          
          S=35;
          drawtext(size->S,align->"mid",c+(.01,0)/5, functions_k, color->[1,1,1]);
          drawtext(size->S,align->"mid",c+(-.01,0)/5, functions_k, color->[1,1,1]);
          drawtext(size->S,align->"mid",c+(0,.01)/5, functions_k, color->[1,1,1]);
          drawtext(size->S,align->"mid",c+(0,-.01)/5, functions_k, color->[1,1,1]);
          drawtext(size->S,align->"mid",c, functions_k, color->  if(round(mk)==k,[.8,0.5,0],[0,0,0]));
      );
    );    
  );
));

drawtext((.25,.25), msg);


</script>


<script id="csmouseclick" type="text/x-cindyscript">
errc("click");
cline = 0;
forall(nodes, line, cline = cline+1; forall(line, node,
  v = mouse() - node_1;
  if(|v.x|<imgwidth/2 & |v.y|<imgwidth/2,
    //drawimage(mouse()+(-1,-0.5), mouse()+(1,-.5), node_2);
    
    if(mode=="free",
      mk = mod(round(arctan2(mouse()-node_1)*length(functions)/(2*pi))-1,length(functions))+1;
      if(functions_mk=="✖",
        if(cline>=2,
          delete(node);
        );
        
        ,
        add(functions_mk);  
      );
    );
    
    

    if(mode=="+",
      curinputs = curinputs :> node;
      if(length(curinputs)==nargs,
        if(addfun=="⧖",
          addnode("⧖1", curinputs, cline+1);
          addnode("⧖2", curinputs, cline+1);
          ,
          addnode(addfun, curinputs, cline+1);
        );
        curinputs = [];  
      );
    );
    
  )
));

</script>

<script type="text/javascript">

var cdy = CindyJS({
  ports: [{
    id: "CSCanvas",
    grid: .1,
    fill: "window",
    background: "rgb(200,200,200)",
    transform:[{visibleRect:[0.2,0.2,10.5,5.8]}]
  }],
  scripts: "cs*",
  language: "en",
  images: {
    "pic1": "pic1.png",
    "pic2": "pic2.png",
    "pic3": "pic3.png",
    "pic4": "pic4.png"
  },
  geometry: []
});

</script>
</head>

<body>
  <div id="CSCanvas"></div>
  <button style="position: absolute; top:10px; right: 10px;" onclick="location.reload()">reset</button>
</body>

</html>
